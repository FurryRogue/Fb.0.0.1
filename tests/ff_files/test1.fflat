
// Midi instruments denoted by (preset value, volume)
midi violin    = 238, 110
midi synth     =  75, 100
midi metronome = 243, 90

// The basic rythmic building blocks in the piece
rythm lead1   (4 / 4) || 4 4 4 4 | 4 4 2   ||
rythm lead2   (4 / 4) || 4 4 4 4 | 2 4 r4  ||
rythm sblock  (4 / 4) || shuffle | shuffle ||
rythm pblock  (4 / 4) || poly3_4 | poly3_4 ||
rythm pblock2 (4 / 4) |: t4 t2 t2 t4 :|
rythm combob  (4 / 4) || shuffle8 4 4 | poly3_4 ||

// Some rythmic phrases
phrase shuffle8  = | t8 (t8 t8) (t8 t8) t8 |
phrase shuffle   = | shuffle8   shuffle8   |
phrase poly3_4   = | t2     t2     t2      |

// Lille peter edderkop
phrase ph1  = { 1 1 1 2  3 3 3 }
phrase ph2  = { 2 2 2 3  1 1   }
phrase ph3  = { 2 2 2 3- 1 1   }

// The melodic building blocks for the piece
melody e1   violin { E- } phrygian   { end  }
melody e2   violin { G  } mixolydian { end  }
melody e3   synth  { B- } locrian    { end  }
melody e12  violin { E  } phrygian   { end2 }
melody e22  violin { E  } phrygian   { end3 }
melody e32  synth  { E  } phrygian   { end4 }
melody lpe1 violin { C  } ionian     { ph1  }
melody lpe2 violin { C  } ionian     { ph2  }
melody lpe3 violin { C  } ionian     { ph3  }
melody lpe4 synth  { C  } ionian     { ph1  }
melody lpe5 synth  { C  } ionian     { ph2  }

// A dummy melody for the metronome
melody m  metronome { C } ionian { C }

// A set of chords for the ending
phrase end =  { 2 5 1 }
phrase end2 = { 4 5 8 }
phrase end3 = { 6 7 6 }
phrase end4 = { 1 2 3 }

// some rythms for the ending
rythm tada  (3/4) || (2 4) | r8 (8  2) | 2. ||
rythm tada2 (4/4) || 1 | 1 | 1 ||
rythm standard (4/4) 3 |: 4 4 4 4 :|

// A classical danish children song
composition lille_peter_edderkop =
     ({ C- } ionian   lpe1) <- lead1  +
     ({ E  } phrygian lpe1) <- lead1  + m <- sblock ::
     ({ C- } ionian   lpe2) <- lead2  +
     ({ E  } phrygian lpe2) <- lead2  + m <- sblock

// A cast to some minor keys with a 3 over 4 feel metronome
composition first_variation =
     ({ D-  } aeolian    lpe1) <- lead1  +
     ({ F   } ionian     lpe1) <- lead1  + m <- pblock  ::
     ({ E   } locrian    lpe2) <- pblock +
     ({ G-  } dorian     lpe2) <- sblock + m <- pblock  ::
     ({ C-  } aeolian    lpe1) <- lead2  +
     ({ Eb  } ionian     lpe1) <- lead2  + m <- pblock

// Synthesiser appegio for the second variation
melody apegs synth { D } dorian { 8- 4 3 2 3 6 7 }
rythm run  (1 / 4) 8 |: 16 16 16 16 :|
rythm rund (1 / 4) 7 |: 16 16 16 16 :|

// An apegiated cast of the melody in some diatonic keys
composition second_variation = // with synthesiser
     ({ E-  } lydian   lpe1) <- lead2  +
     ({ B   } ionian  apegs) <- run    +
     ({ Ab- } aeolian  lpe4) <- lead2  ::
     ({ Ab  } lydian   lpe2) <- lead2  +
     ({ Ab  } ionian  apegs) <- rund   +
     ({ C   } aeolian  lpe2) <- lead2  + m <- combob

// The 8-bit game boy poly tone classic
composition third_variation = // busy violins
     ({ C  } ionian   lpe1) <- rund   +
     ({ B  } locrian  lpe4) <- lead1  +
     ({ E- } phrygian lpe1) <- rund   + m <- pblock2 ::
     ({ D- } dorian   lpe1) <- run    +
     ({ A  } aeolian  lpe5) <- lead2  +
     ({ E  } phrygian lpe1) <- run    + m <- pblock2

// Some epic chords for the ending
composition ending =
     ({ C- } ionian   lpe5) <- lead1  +
     ({ D+ } dorian   lpe4) <- lead1  +
     ({ F  } lydian   lpe1) <- lead1  +
     ({ E  } phrygian lpe4) <- lead1  + e1 <- run + m <- pblock   ::
     e1  <- tada   + e2   <- tada   + e3  <- tada  + m <- pblock2  +
     e1  <- run    + lpe5 <- tada                                  ::
     e12 <- tada2  + e22  <- tada2  + e32 <- tada2 + m <- standard +
     e12 <- rund   + lpe5 <- rund

// You can comment out pieces from main if you don't want to hear them
composition main =
     lille_peter_edderkop ::
     lille_peter_edderkop ::
     first_variation      ::
     second_variation     ::
     third_variation      ::
     ending